plugins {
    id 'groovy'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
}
javadoc {
    title = "AndroidAop Plugin ${version} API"
    options.bottom = 'AndroidAop'
}
apply from: "$rootDir/gradle/java_base.gradle"


dependencies {
    implementation gradleApi()
    implementation localGroovy()
    compileOnly libs.gradle
    implementation libs.asm.lib
    implementation libs.asm.commons
    implementation libs.flyjingfish.javassist
    implementation libs.gson
    implementation project(path: ':android-aop-annotation')
    compileOnly libs.kotlin.gradle.plugin
    implementation libs.kotlinx.coroutines
    implementation libs.fastTransform
    implementation libs.kotlinx.metadata.jvm
}
// 发布到gradlePluginPortal
if (publishGradle == "1"){
    apply plugin: "com.gradle.plugin-publish"
    gradlePlugin {
        website = PROJ_WEBSITEURL
        vcsUrl = PROJ_VCSURL
        plugins {
            androidAop {
                // 插件id
                id = PROJ_GROUP + '.androidaop'
                // 插件全路径
                implementationClass = "com.flyjingfish.android_aop_plugin.AndroidAopPlugin"
                // 插件名称
                displayName = 'AndroidAOP Plugin'
                //描述
                description = PROJ_DESCRIPTION
                //标签
                tags.addAll('AndroidAop', 'FlyJingFish', 'android', 'aop')
            }
        }
    }
}else{
    apply from: "$rootDir/gradle/java_publish.gradle"
}

